pluginManagement {
    repositories {
        maven {
            url = 'https://maven.lukebemish.dev/releases/'
        }
        gradlePluginPortal()
    }
}

plugins {
    id 'dev.lukebemish.managedversioning' version '2.0.0-beta.1'
    id 'dev.lukebemish.conventions' version '0.2.0-beta.1'
}

dependencyResolutionManagement {
    repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

managedVersioning {
    versionFile.set file('version.properties')
    versionPRs()
    versionSnapshots()

    publishing {
        mavenSnapshot()
        mavenPullRequest()
    }

    gitHubActions {
        release {
            prettyName = 'Release'
            workflowDispatch = true
            gradleJob {
                name.set 'build'
                buildCache()
                setupGitUser()
                readOnly = false
                gradlew 'Tag Release', 'tagRelease'
                gradlew 'Build', 'build'
                push()
                recordVersion 'Record Version', 'version'
                dependencySubmission()
            }
            gradleJob {
                name.set 'publishPlugins'
                buildCache()
                needs.add('build')
                // TODO: see https://github.com/gradle/plugin-portal-requests/issues/155
                gradlew 'Publish Plugins', 'publishPlugins', '--no-configuration-cache'
                tag.set('${{needs.build.outputs.version}}')
                pluginPortal()
            }
        }
        snapshot {
            prettyName.set 'Snapshot'
            workflowDispatch.set(true)
            onBranches.add 'main'
            gradleJob {
                name.set 'build'
                buildCache()
                cacheReadOnly = false
                gradlew 'Build', 'build'
                gradlew 'Publish', 'publish'
                mavenSnapshot('github')
            }
        }
        build_pr {
            prettyName.set 'Build PR'
            pullRequest.set(true)
            gradleJob {
                name.set 'build'
                gradlew 'Build', 'build'
                gradlew 'Publish', 'publish'
                pullRequestArtifact()
            }
        }
        publish_pr {
            prettyName.set 'Publish PR'
            publishPullRequestAction(
                'github',
                "dev/lukebemish/central-portal-publishing,dev/lukebemish/central-portal-publishing/*",
                'Build PR'
            )
        }
    }
}

rootProject.name = 'central-portal-publishing'

